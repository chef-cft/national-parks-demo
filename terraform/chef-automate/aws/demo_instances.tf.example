resource "aws_instance" "aws-centos7-production" {
  connection {
    user        = "centos"
    private_key = "${file("${var.aws_key_pair_file}")}"
  }

  ami                         = "${data.aws_ami.centos7.id}"
  instance_type               = "t2.micro"
  key_name                    = "${var.aws_key_pair_name}"
  subnet_id                   = "${aws_subnet.habmgmt-subnet-a.id}"
  vpc_security_group_ids      = ["${aws_security_group.chef_automate.id}"]
  associate_public_ip_address = true

  tags {
    Name          = "aws_centos_production_${random_id.instance_id.hex}"
    X-Dept        = "${var.tag_dept}"
    X-Customer    = "${var.tag_customer}"
    X-Project     = "${var.tag_project}"
    X-Application = "${var.tag_application}"
    X-Contact     = "${var.tag_contact}"
    X-TTL         = "${var.tag_ttl}"
  }

  provisioner "file" {
    content     = "${data.template_file.install_hab.rendered}"
    destination = "/tmp/install_hab.sh"
  }

  provisioner "file" {
    content     = "${data.template_file.sup_service.rendered}"
    destination = "/home/centos/hab-sup.service"
  }

  provisioner "file" {
    source     = "files/linux_baseline.toml"
    destination = "/home/centos/linux_baseline.toml"
  }

  provisioner "file" {
    source     = "files/chef-base.toml"
    destination = "/home/centos/chef-base.toml"
  }

  provisioner "remote-exec" {
    inline = [
      "sudo rm -rf /etc/machine-id",
      "sudo systemd-machine-id-setup",
      "sudo hostname aws-centos7",
      "sudo groupadd hab",
      "sudo adduser hab -g hab",
      "chmod +x /tmp/install_hab.sh",
      "sudo /tmp/install_hab.sh",
      "sudo mv /home/centos/hab-sup.service /etc/systemd/system/hab-sup.service",
      "sudo systemctl daemon-reload",
      "sudo systemctl start hab-sup",
      "sudo systemctl enable hab-sup",
      "sleep 60",
      "sudo hab svc load ${var.origin}/linux_baseline --channel stable --strategy at-once",
      "sudo hab config apply linux_baseline.default $(date +%s) /home/centos/linux_baseline.toml",
      "sudo hab svc load ${var.origin}/chef-base --channel stable --strategy at-once",
      "sudo hab config apply chef-base.default $(date +%s) /home/centos/chef-base.toml",
    ]
  }
}

////////////////////////////////
// Templates

data "template_file" "sup_service" {
  template = "${file("${path.module}/templates/hab-sup.service")}"

  vars {
    flags = "--auto-update --listen-gossip 0.0.0.0:9638 --listen-http 0.0.0.0:9631"
  }
}

data "template_file" "install_hab" {
  template = "${file("${path.module}/templates/install-hab.sh")}"
}

